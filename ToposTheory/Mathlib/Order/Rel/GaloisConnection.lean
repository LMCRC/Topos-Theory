/-
Copyright (c) 2024 Lagrange Mathematics and Computing Research Center. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anthony Bordg
-/
import Mathlib.Data.Rel
import Mathlib.Order.GaloisConnection
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback
import ToposTheory.GrothendieckSubtopos
import ToposTheory.Saturation

/-!
# The Galois Connection Induced by a Relation

In this file, we show that an arbitrary relation `R` between a pair of types `Œ±` and `Œ≤` defines
a pair `toDual ‚àò R.leftDual` and `R.rightDual ‚àò ofDual` of adjoint order-preserving maps between the
corresponding posets `Set Œ±` and `(Set Œ≤)·µí·µà`.
We define `R.leftFixedPoints` (resp. `R.rightFixedPoints`) as the set of fixed points `J`
(resp. `I`) of `Set Œ±` (resp. `Set Œ≤`) such that `rightDual (leftDual J) = J`
(resp. `leftDual (rightDual I) = I`).

## Main Results

‚ãÜ `Rel.gc_leftDual_rightDual`: we prove that the maps `toDual ‚àò R.leftDual` and
  `R.rightDual ‚àò ofDual` form a Galois connection.
‚ãÜ `Rel.equivFixedPoints`: we prove that the maps `R.leftDual` and `R.rightDual` induce inverse
  bijections between the sets of fixed points.

## References

‚ãÜ Engendrement de topologies, d√©montrabilit√© et op√©rations sur les sous-topos, Olivia Caramello and
  Laurent Lafforgue (in preparation)

## Tags

relation, Galois connection, induced bijection, fixed points
-/

namespace Rel

variable {Œ± Œ≤ : Type*} (R : Rel Œ± Œ≤)

/-! ### Pairs of adjoint maps defined by relations -/

open OrderDual

/-- `leftDual` maps any set `J` of elements of type `Œ±` to the set `{b : Œ≤ | ‚àÄ a ‚àà J, R a b}` of
elements `b` of type `Œ≤` such that `R a b` for every element `a` of `J`. -/
def leftDual (J : Set Œ±) : Set Œ≤ := {b : Œ≤ | ‚àÄ ‚¶Éa‚¶Ñ, a ‚àà J ‚Üí R a b}

/-- `rightDual` maps any set `I` of elements of type `Œ≤` to the set `{a : Œ± | ‚àÄ b ‚àà I, R a b}`
of elements `a` of type `Œ±` such that `R a b` for every element `b` of `I`. -/
def rightDual (I : Set Œ≤) : Set Œ± := {a : Œ± | ‚àÄ ‚¶Éb‚¶Ñ, b ‚àà I ‚Üí R a b}

/-- The pair of functions `toDual ‚àò leftDual` and `rightDual ‚àò ofDual` forms a Galois connection. -/
theorem gc_leftDual_rightDual : GaloisConnection (toDual ‚àò R.leftDual) (R.rightDual ‚àò ofDual) :=
  fun J I ‚Ü¶ ‚ü®fun h a ha b hb ‚Ü¶ h (by simpa) ha, fun h b hb a ha ‚Ü¶ h (by simpa) hb‚ü©

/-! ### Induced equivalences between fixed points -/

/-- `leftFixedPoints` is the set of elements `J : Set Œ±` satisfying `rightDual (leftDual J) = J`. -/
def leftFixedPoints := {J : Set Œ± | R.rightDual (R.leftDual J) = J}

/-- `rightFixedPoints` is the set of elements `I : Set Œ≤` satisfying `leftDual (rightDual I) = I`.
-/
def rightFixedPoints := {I : Set Œ≤ | R.leftDual (R.rightDual I) = I}

open GaloisConnection

/-- `leftDual` maps every element `J` to `rightFixedPoints`. -/
theorem leftDual_mem_rightFixedPoint (J : Set Œ±) : R.leftDual J ‚àà R.rightFixedPoints := by
  apply le_antisymm
  ¬∑ apply R.gc_leftDual_rightDual.monotone_l; exact R.gc_leftDual_rightDual.le_u_l J
  ¬∑ exact R.gc_leftDual_rightDual.l_u_le (R.leftDual J)

/-- `rightDual` maps every element `I` to `leftFixedPoints`. -/
theorem rightDual_mem_leftFixedPoint (I : Set Œ≤) : R.rightDual I ‚àà R.leftFixedPoints := by
  apply le_antisymm
  ¬∑ apply R.gc_leftDual_rightDual.monotone_u; exact R.gc_leftDual_rightDual.l_u_le I
  ¬∑ exact R.gc_leftDual_rightDual.le_u_l (R.rightDual I)

/-- The maps `leftDual` and `rightDual` induce inverse bijections between the sets of fixed points.
-/
def equivFixedPoints : R.leftFixedPoints ‚âÉ R.rightFixedPoints where
  toFun := fun ‚ü®J, _‚ü© => ‚ü®R.leftDual J, R.leftDual_mem_rightFixedPoint J‚ü©
  invFun := fun ‚ü®I, _‚ü© => ‚ü®R.rightDual I, R.rightDual_mem_leftFixedPoint I‚ü©
  left_inv J := by obtain ‚ü®_, hJ‚ü© := J; rw [Subtype.mk.injEq, hJ]
  right_inv I := by obtain ‚ü®_, hI‚ü© := I; rw [Subtype.mk.injEq, hI]

theorem rightDual_leftDual_le_of_le {J J' : Set Œ±} (h : J' ‚àà R.leftFixedPoints) (h‚ÇÅ : J ‚â§ J') :
    R.rightDual (R.leftDual J) ‚â§ J' := by
  rw [‚Üê h]
  apply R.gc_leftDual_rightDual.monotone_u
  apply R.gc_leftDual_rightDual.monotone_l
  exact h‚ÇÅ

theorem leftDual_rightDual_le_of_le {I I' : Set Œ≤} (h : I' ‚àà R.rightFixedPoints) (h‚ÇÅ : I ‚â§ I') :
    R.leftDual (R.rightDual I) ‚â§ I' := by
  rw [‚Üê h]
  apply R.gc_leftDual_rightDual.monotone_l
  apply R.gc_leftDual_rightDual.monotone_u
  exact h‚ÇÅ

end Rel

open CategoryTheory

namespace Subtopos

/-! ### The induced duality of topologies and subtoposes -/

universe u

variable {C : Type u} [SmallCategory C]

open Limits NatTrans Rel

def restrictionMap {X : C} (P : C·µí·µñ ‚•§ Type u)  (S : Sieve X) {X' : C} (f : X' ‚ü∂ X) :
    (yoneda.obj X' ‚ü∂ P) ‚Üí ((Sieve.pullback f S).functor ‚ü∂ P) :=
  ((Sieve.pullback f S).functorInclusion ‚â´ .)

def bij_of_restrictionMap (XS : (X : C) √ó Sieve X) (P : C·µí·µñ ‚•§ Type u) : Prop :=
  ‚àÄ {X' : C} (f : X' ‚ü∂ XS.1), Function.Bijective (restrictionMap P XS.2 f)

theorem bij_of_restrictionMap_iff_isSheafFor {X : C} (S : Sieve X) :
    (‚àÄ {X' : C} (f : X' ‚ü∂ X), Presieve.IsSheafFor P (Sieve.pullback f S).arrows) ‚Üî bij_of_restrictionMap ‚ü®X, S‚ü© P := by
  conv =>
    lhs
    ext X' f
    rw [Presieve.isSheafFor_iff_yonedaSheafCondition]
    unfold Presieve.YonedaSheafCondition
  conv =>
    rhs
    unfold bij_of_restrictionMap
    simp [restrictionMap, Function.bijective_iff_existsUnique]

theorem mem_leftFixedPoint (J : GrothendieckTopology C) :
    {‚ü®X, S‚ü© : (X : C) √ó Sieve X | S ‚àà J.sieves X} ‚àà (leftFixedPoints bij_of_restrictionMap) := by
  ext ‚ü®X, S‚ü©
  simp [leftFixedPoints, leftDual, rightDual]
  apply Iff.intro
  . rw [‚Üê Presheaf.sheaves_respect_iff_covering]
    intros h P hP
    have: (‚àÄ {X' : C} (f : X' ‚ü∂ X), Presieve.IsSheafFor P (Sieve.pullback f S).arrows) := by
      rw [bij_of_restrictionMap_iff_isSheafFor]
      apply h
      intros YS hYS
      obtain ‚ü®Y, S'‚ü© := YS
      rw [‚Üê bij_of_restrictionMap_iff_isSheafFor]
      intros _ f
      exact hP.isSheafFor (Sieve.pullback f S') (J.pullback_stable f hYS)
    have := this (ùüô _)
    rw [Sieve.pullback_id] at this
    exact this
  . tauto

def yoneda_iso_top_functor (X : C) : yoneda.obj X ‚âÖ Sieve.functor (X := X) ‚ä§ :=
  NatIso.ofComponents (fun X' ‚Ü¶ by simp; exact Equiv.toIso {
    toFun := fun f ‚Ü¶ ‚ü®f, trivial‚ü©
    invFun := fun g ‚Ü¶ g.1
    left_inv := by tauto
    right_inv := by tauto
  })

lemma bij_of_restrictionMap_top (X : C) (P : C·µí·µñ ‚•§ Type u) : bij_of_restrictionMap ‚ü®X, ‚ä§‚ü© P := by
  unfold bij_of_restrictionMap restrictionMap
  intros X' f
  rw [Function.bijective_iff_has_inverse]
  use (fun g ‚Ü¶ by
    simp at g
    exact (yoneda_iso_top_functor X').hom ‚â´ g)
  aesop_cat

lemma bij_of_restrictionMap_pullback {X : C} (S : Sieve X) (P : C ·µí·µñ ‚•§ Type u)
    (h : bij_of_restrictionMap ‚ü®X, S‚ü© P) {Y : C} (f : Y ‚ü∂ X) : bij_of_restrictionMap ‚ü®Y, Sieve.pullback f S‚ü© P := by
  unfold bij_of_restrictionMap restrictionMap
  intros Z g
  simp
  rw [‚Üê Sieve.pullback_comp]
  exact h (g ‚â´ f)

instance instGrothendieckTopologyOfleftFixedPoint {J : Set ((X : C) √ó Sieve X)}
    (h : J ‚àà leftFixedPoints bij_of_restrictionMap) : GrothendieckTopology C := by
  simp [leftFixedPoints, rightDual, leftDual] at h
  exact {
    sieves := fun X ‚Ü¶ {S : Sieve X | ‚ü®X, S‚ü© ‚àà J}
    top_mem' := by
      intros X
      rw [‚Üê h]
      intros P _
      exact bij_of_restrictionMap_top X P
    pullback_stable' := by
      intros _ _ S f hS
      rw [‚Üê h]
      intros P hP
      exact bij_of_restrictionMap_pullback S P (hP hS) f
    transitive' := by
      intros _ S hS R hR
      rw [‚Üê h]
      intros P hP

      unfold bij_of_restrictionMap restrictionMap
      intros Z f
      sorry
  }

open GrothendieckTopos

variable {I : Set (C·µí·µñ ‚•§ Type u)}

theorem mem_rightFixedPoint (‚Ñ∞ : Subtopos (C·µí·µñ ‚•§ Type u)) (h : ‚àÄ P, ‚Ñ∞.obj P ‚Üî P ‚àà I) :
    I ‚àà rightFixedPoints bij_of_restrictionMap := by admit

instance subtopos_of_rightFixedPoint (h : I ‚àà rightFixedPoints bij_of_restrictionMap) :
    Subtopos (C·µí·µñ ‚•§ Type u) where
  obj P := P ‚àà I
  adj := sorry
  flat := sorry
  mem := sorry

instance : GrothendieckTopology C ‚âÉ Subtopos (C·µí·µñ ‚•§ Type u) := sorry

end Subtopos
